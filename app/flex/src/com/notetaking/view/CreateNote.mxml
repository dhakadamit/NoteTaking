<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init();" show="clearAllFields();">

	<mx:Script>
		<![CDATA[
			import com.notetaking.model.NoteTakingApplicationModelLocator;
			import mx.managers.PopUpManager;
			import com.notetaking.model.Tag;
			import com.notetaking.model.Note;
			import com.notetaking.event.CreateNoteEvent;
			import mx.collections.ArrayCollection;
			
			[Bindable]
			private var model:NoteTakingApplicationModelLocator = NoteTakingApplicationModelLocator.getInstance();
			
			private function init():void {
				submit.addEventListener(MouseEvent.CLICK, createNewNote);
			}
			
			private function clearAllFields():void {
				title.text = "";
				content.htmlText = "";
				tags.text = "";
			}
			
			private function createNewNote(event:Event):void {
				var note:Note = new Note();
				note.title = title.text;
				note.content = content.htmlText;
				note.plainContent = content.text;
				note.tags = tagList();
				
				PopUpManager.addPopUp(model.progressBar, this, true);
				PopUpManager.centerPopUp(model.progressBar);
				new CreateNoteEvent(note).dispatch();
			}
			
			private function tagList():ArrayCollection {
				var names:ArrayCollection = new ArrayCollection(tags.text.split(" "));
				var tags:ArrayCollection = new ArrayCollection();
				for each(var name:String in names) {
					var tag:Tag = new Tag();
					tag.name = name;
					tags.addItem(tag);
				}	
				return tags;
			}
		]]>
	</mx:Script>
	
	<mx:VBox left="20" right="20" top="20" bottom="20" verticalGap="20">
		<mx:HBox width="100%">
			<mx:Label text="Title" fontWeight="bold" fontSize="12"/>
			<mx:TextInput id="title" width="70%"/>
		</mx:HBox>
		
		<mx:RichTextEditor id="content" width="100%" height="100%"/>		
		
		<mx:HBox width="100%">
			<mx:Label text="Tags" fontWeight="bold" fontSize="12"/>
			<mx:TextInput id="tags" width="70%"/>
		</mx:HBox>
		<mx:Button id="submit" label="Create"/>	
	</mx:VBox>
	
</mx:Canvas>
