<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas width="100%"
	 xmlns:mx="http://www.adobe.com/2006/mxml"
	 creationComplete="init();" show="init();" 
	 xmlns:comps="com.notetaking.view.components.centerBody.noteslisting.*">
		
		<mx:Script>
			<![CDATA[
				import com.notetaking.model.Note;
				import com.notetaking.event.NotesTotalCountEvent;
				import mx.managers.PopUpManager;
				import mx.events.ListEvent;
				import mx.events.ItemClickEvent;
				import com.notetaking.utils.PaginationManager;
				import com.notetaking.event.GetAllNotesEvent;
				import com.notetaking.model.NoteTakingApplicationModelLocator;
				
				[Bindable]
				private var model:NoteTakingApplicationModelLocator = NoteTakingApplicationModelLocator.getInstance();
				[Bindable]
				private var paginationManager:PaginationManager = new PaginationManager(getAllNotes, totalCount);
				
				private var _query:String = "";
				private var _tag:String = "";
				
				private function init():void {
					clearParameters();
					paginationComponent.initializePaginationManager();
					model.updateNotesListing = this.findAll;
				} 	
								
				public function findAll(event:Event = null):void{
					clearParameters();
					paginationComponent.initializePaginationManager();
				}
				
				public function search(query:String):void {
					clearParameters();
					this._query = query;
					paginationComponent.initializePaginationManager();
				}
				
				public function fetchNotesTaggedWith(tag:String):void {
					clearParameters();
					this._tag = tag;
					paginationComponent.initializePaginationManager();
				}
				
				private function clearParameters():void {
					this._query = "";
					this._tag = "";
				}

				private function getAllNotes(pageNumber:Number, callback:Function):void {
					PopUpManager.addPopUp(model.progressBar, this, false);
					PopUpManager.centerPopUp(model.progressBar);
				
					new GetAllNotesEvent(pageNumber, _query, "", _tag).dispatch();
					new NotesTotalCountEvent(callback, _query, "", _tag).dispatch();
				}
				
				private function totalCount():Number {
					return model.notesTotalCount;
				}
				
				private function showItem(event:ListEvent):void {
					model.selectedNote = Note (event.itemRenderer.data);
				}
					
			]]>
		</mx:Script>
			
	<mx:VBox width="100%" height="100%">				
		<mx:DataGrid id="notes" dataProvider="{model.notes}" rowCount="10" 
				width="100%" rowHeight="20" verticalScrollPolicy="off" horizontalScrollPolicy="off"
				itemClick="showItem(event);" fontFamily="Verdana" fontSize="10" headerHeight="0">
			<mx:columns>
				<mx:DataGridColumn dataField="title" wordWrap="false"/>	
			</mx:columns>
		</mx:DataGrid>
		
		<comps:PaginationComponent id="paginationComponent" paginationManager="{paginationManager}"/>		
	</mx:VBox>
</mx:Canvas>
